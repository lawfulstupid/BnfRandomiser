syntax = line* -- structure for the whole file
line = ws? line-content ws? eol
line-content = ws? | rule | command
rule = symbol ws? '=' ws? expression | symbol ws? '<-' ws? string
expression = sequence | sequence ws? '|' ws? expression
sequence = term term-mod? | term term-mod? ws sequence
term = symbol | literal | backref
term-mod = '?' | '+' | '*'
symbol = letter | symbol symchar
literal = "'" string1 "'" | '"' string2 '"'
backref = '$' digit+
string = string1 | string2
string1 = char1*
string2 = char2*
letter <- letters.txt
digit = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
symchar = letter :50 | digit :50 | '_' :1 | '-' :1
anychar = symchar:10 | '|' | ' ' | '!' | '#' | '$' | '%' | '&' | '(' | ')' | '*' | '+' | ',' | '.' | '/' | ':' | ';' | '>' | '=' | '<' | '?' | '@' | '[' | '\' | ']' | '^' | '`' | '{' | '}' | '~'
char1 = anychar | '"'
char2 = anychar | "'"
ws = ' '+
eol = '\n' | '\r\f'
command = '!' ws? 'generate' ws symbol

!generate rule -- when this file is run, a randomly generated rule will be output