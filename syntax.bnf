syntax = line | line syntax -- structure for the whole file
line = opt-ws line-content opt-ws eol
line-content = opt-ws | rule | '!' opt-ws command
rule = symbol opt-ws '=' opt-ws expression
expression = sequence | sequence opt-ws '|' opt-ws expression
sequence = term | term ws sequence
term = symbol | literal
symbol = letter | symbol symchar
literal = "'" string1 "'" | '"' string2 '"'
string1 = '' | char1 string1
string2 = '' | char2 string2
letter <- "letters.txt"
digit = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
symchar = letter | digit | '_' | '-'
anychar = symchar | '|' | ' ' | '#' | '$' | '%' | '&' | '(' | ')' | '*' | '+' | ',' | '.' | '/' | ':' | ';' | '>' | '=' | '<' | '?' | '@' | '[' | '\' | ']' | '^' | '`' | '{' | '}' | '~'
char1 = anychar | '"'
char2 = anychar | "'"
ws = ' ' opt-ws
opt-ws = '' | ws
eol = '\n' | '\r\f'
command = 'generate' ws symbol

!generate rule -- when this file is run, a randomly generated rule will be output